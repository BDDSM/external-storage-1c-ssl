Функция внеш_хф_ПолучитьУникальноеИмяСПутем(ИмяКаталога, ИмяФайла, ТомХранения = Неопределено) Экспорт
	
	ИтоговыйПуть = "";
	
	Счетчик = 0;
	ЦиклНомер = 0;
	Успешно = Ложь;
	КодБукваA = КодСимвола("A", 1);
	
	ГенераторСлучая = Неопределено;
	
#Если Не ВебКлиент Тогда
	ГенераторСлучая = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
#КонецЕсли

	КоличествоСлучайныхВариантов = 26;
	
	Пока НЕ Успешно И ЦиклНомер < 100 Цикл
		НомерКаталога = 0;
		
#Если Не ВебКлиент Тогда
		НомерКаталога = ГенераторСлучая.СлучайноеЧисло(0, КоличествоСлучайныхВариантов - 1);
#Иначе
		НомерКаталога = ТекущаяУниверсальнаяДатаВМиллисекундах() % КоличествоСлучайныхВариантов;
#КонецЕсли

		Если Счетчик > 1 И КоличествоСлучайныхВариантов < 26 * 26 * 26 * 26 * 26 Тогда
			КоличествоСлучайныхВариантов = КоличествоСлучайныхВариантов * 26;
		КонецЕсли;
		
		БуквыКаталога = "";
		КодБукваA = КодСимвола("A", 1);
		
		Пока Истина Цикл
			НомерБуквы = НомерКаталога % 26;
			НомерКаталога = Цел(НомерКаталога / 26);
			
			КодКаталога = КодБукваA + НомерБуквы;
			
			БуквыКаталога = БуквыКаталога + Символ(КодКаталога);
			Если НомерКаталога = 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		ПодКаталог = ""; // Частичный путь.
		
		// По умолчанию вначале используется корень, если возможности нет,
		// то добавляется A, B, ... Z, .. ZZZZZ, .. AAAAA, .. AAAAAZ и т.д.
		Если  Счетчик = 0 Тогда
			ПодКаталог = "";
		Иначе
			ПодКаталог = БуквыКаталога;
			ЦиклНомер = Окр(Счетчик / 26);
			
			Если ЦиклНомер <> 0 Тогда
				ЦиклНомерСтрока = Строка(ЦиклНомер);
				ПодКаталог = ПодКаталог + ЦиклНомерСтрока;
			КонецЕсли;
			
			Если ЭтоЗарезервированноеИмяКаталога(ПодКаталог) Тогда
				Продолжить;
			КонецЕсли;
			
			ПодКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПодКаталог);
		КонецЕсли;
		
		ПолныйПодКаталог = ИмяКаталога + ПодКаталог;
		
		//--
		ТипКаталога = внеш_хф_ТипКаталога(ИмяКаталога);
		
		Если ТипКаталога = "СетевойКаталог" Тогда
			
			// Создание каталога для файлов.
			КаталогНаДиске = Новый Файл(ПолныйПодКаталог);
			Если НЕ КаталогНаДиске.Существует() Тогда
				Попытка
					СоздатьКаталог(ПолныйПодКаталог);
				Исключение
					ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Ошибка при создании каталога ""%1"":
					|""%2"".'"),
					ПолныйПодКаталог,
					КраткоеПредставлениеОшибки(ИнформацияОбОшибке()) );
				КонецПопытки;
			КонецЕсли;
		КонецЕсли;
		
		ФайлПопытки = ПолныйПодКаталог + ИмяФайла;
		Счетчик = Счетчик + 1;
		
		Если ТипКаталога = "СетевойКаталог" Тогда
			
			// Проверка, есть ли файл с таким именем.
			ФайлНаДиске = Новый Файл(ФайлПопытки);
			Если НЕ ФайлНаДиске.Существует() Тогда  // Нет такого файла.
				ИтоговыйПуть = ПодКаталог + ИмяФайла;
				Успешно = Истина;
			КонецЕсли;
		ИначеЕсли ТипКаталога = "SelectelSwiftAPI" Тогда	
			
			Если НЕ внеш_хф_SelectelSwiftAPI.ПроверитьФайлСуществует(ФайлПопытки, ТомХранения) Тогда
				ИтоговыйПуть = ПодКаталог + ИмяФайла;
				Успешно = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		//--
		
	КонецЦикла;
	
	Возврат ИтоговыйПуть;
	
КонецФункции

Функция внеш_хф_ТипКаталога(ИмяКаталога)
	
	Если СтрНайти(ИмяКаталога, внеш_хф_SelectelSwiftAPI.БазовыйАдрес()) > 0 Тогда
		Возврат "SelectelSwiftAPI";
	Иначе
		Возврат "СетевойКаталог";
	КонецЕсли;
КонецФункции
